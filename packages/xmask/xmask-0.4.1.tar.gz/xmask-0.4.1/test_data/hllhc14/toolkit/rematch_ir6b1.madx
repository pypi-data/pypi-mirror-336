scale = 23348.89927;
scmin := 0.03*7000./nrj;
sch=0.99; if (is_thin==1){sch=1;};
qtlimit2 := sch*160.0/scale;
qtlimit3 := sch*200.0/scale;
qtlimit4 := sch*125.0/scale;
qtlimit5 := sch*120.0/scale;
qtlimit6 := sch*90.0/scale;


if(on_holdselect==0){ exec,select(6,56,67,b1); };

if(on_fixedip==1){
muxip6b1 = 2.0000 ;betxip6b1= 187.297499 ;alfxip6b1 = -0.541994 ;
muyip6b1 = 2.0300 ;betyip6b1= 168.122917 ;alfyip6b1 =  0.605891 ;
dxip6b1 =  0.1400 ;dpxip6b1 =   0 ;
};
value,muxip6b1,betxip6b1,alfxip6b1;
value,muyip6b1,betyip6b1,alfyip6b1;
value,dxip6b1,dpxip6b1;

use,sequence=lhcb1,range=s.ds.l6.b1/e.ds.r6.b1;
match,      sequence=lhcb1, beta0=bir6b1;
weight,mux=10,muy=10;
if(nomatch_beta==0){
constraint,sequence=lhcb1,range=ip6,betx=betxip6b1;
constraint,sequence=lhcb1,range=ip6,alfx=alfxip6b1;
constraint,sequence=lhcb1,range=ip6,bety=betyip6b1;
constraint,sequence=lhcb1,range=ip6,alfy=alfyip6b1;
};
if(nomatch_dx==0){
  constraint,sequence=lhcb1,range=ip6,dx =dxip6b1;
};
if(nomatch_dpx==0){
  constraint,sequence=lhcb1,range=ip6,dpx=dpxip6b1;
};
!constraint,range=mqml.10l6.b1,bety< 700;
!constraint,range=mqm.9l6.b1,betx< 1250;
!if(is_thin==1){
!  constraint,sequence=lhcb1,range=ip6,betx=betxip6b1;
!};
constraint,sequence=lhcb1,range=e.ds.r6.b1,alfx=eir6b1->alfx,alfy=eir6b1->alfy;
constraint,sequence=lhcb1,range=e.ds.r6.b1,betx=eir6b1->betx,bety=eir6b1->bety;
constraint,sequence=lhcb1,range=e.ds.r6.b1,dx=eir6b1->dx,dpx=eir6b1->dpx;
constraint,sequence=lhcb1,range=e.ds.r6.b1,   mux=muxip6b1+eir6b1->mux;
constraint,sequence=lhcb1,range=e.ds.r6.b1,   muy=muyip6b1+eir6b1->muy;
if(match_on_aperture==1){
constraint,range=mqml.10l6.b1,bety< 170.+7.2-6+18+epsap;
constraint,range=MQY.5L6.B1,betx<592+epsap;
constraint,range=MQY.4L6.B1,bety<610+6+epsap;
constraint,range=MQY.4R6.B1,betx<552+epsap;
constraint,range=MQY.5R6.B1,bety<600+0+epsap;
constraint,range=mqm.9r6.b1,bety< 180+epsap;
constraint,range=mqml.10r6.b1,betx< 180.+7.3+epsap;
constraint,range=mq.11r6.b1,bety< 178.+2.0+0.3+1.0+epsap;
};
 vary,name=kqt13.l6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l6b1, step=1.0E-6, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
 vary,name=kq10.l6b1,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.l6b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq8.l6b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq5.l6b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
! vary,name=kq4.l6b1,   step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;

 vary,name=kq4.r6b1,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq5.r6b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.r6b1,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.r6b1,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq10.r6b1,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kqtl11.r6b1, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
 vary,name=kqt12.r6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt13.r6b1,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
!lmdif,calls=200,tolerance=1.e-21;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;
endmatch;

exec,mk_irtwiss(6,b1);
chi6_nsig = 10.1;
chi6_dPoverP = 2e-4;
chi6_emitx=2.5e-6/(7000/0.9382720814);
chi6_maxorbitdrift = 0.6e-3;
al_dump=761;

refbetxip6b1=table(twiss,IP6,betx); refalfxip6b1=table(twiss,IP6,alfx);
refbetyip6b1=table(twiss,IP6,bety); refalfyip6b1=table(twiss,IP6,alfy);
refdxip6b1=table(twiss,IP6,dx); refdpxip6b1=table(twiss,IP6,dpx);

refdmuxkickb1_tcsg=table(twiss,TCSP.A4R6.B1,mux)- table(twiss,MKD.H5L6.B1,mux);
refdmuxkickb1_tcdqc=table(twiss,TCDQA.C4R6.B1,mux)-table(twiss,MKD.H5L6.B1,mux);
refdmuxkickb1_tcdqb=table(twiss,TCDQA.B4R6.B1,mux)-table(twiss,MKD.H5L6.B1,mux);
refdmuxkickb1_tcdqa=table(twiss,TCDQA.A4R6.B1,mux)-table(twiss,MKD.H5L6.B1,mux);
refdmuxkickb1=refdmuxkickb1_tcdqc;
if(abs(refdmuxkickb1_tcdqa-0.25)>abs(refdmuxkickb1-0.25)){
refdmuxkickb1=refdmuxkickb1_tcdqa;};
if(abs(refdmuxkickb1_tcdqb-0.25)>abs(refdmuxkickb1-0.25)){
refdmuxkickb1=refdmuxkickb1_tcdqb;};

refdxq5l6b1=abs(table(twiss,MQY.5L6.B1,dx));
refdxq4r6b1=abs(table(twiss,MQY.4R6.B1,dx));

refdxtcdqb1=table(twiss,TCDQA.A4R6.B1,dx);
refbetxtcdqb1=table(twiss,TCDQA.A4R6.B1,betx);
refbetytcdqb1=table(twiss,TCDQA.A4R6.B1,bety);
refbetxtcdsb1=table(twiss,TCDSA.4L6.B1,betx);
refbetytcdsb1=table(twiss,TCDSA.4L6.B1,bety);
refbetxtcsgb1=table(twiss,TCSP.A4R6.B1,betx);
refbetytcsgb1=table(twiss,TCSP.A4R6.B1,bety);
refbetxmkdb1 =table(twiss,MKD.H5L6.B1,betx);
refbetymkdb1 =table(twiss,MKD.H5L6.B1,bety);

refbxdumpb1=refbetxip6b1-2*al_dump*refalfxip6b1+
                        al_dump^2*(1+refalfxip6b1^2)/refbetxip6b1;
refbydumpb1=refbetyip6b1-2*al_dump*refalfyip6b1+
                        al_dump^2*(1+refalfyip6b1^2)/refbetyip6b1;
refbdumpb1=sqrt(refbxdumpb1*refbydumpb1);

refdmuxkickb1_bds=table(twiss,MKD.O5L6.B1,mux);
refdmuxkickb1_bdsa=table(twiss,MKD.A5L6.B1,mux);
refdmuxkickb1_eds=table(twiss,e.ds.r6.b1,mux)- table(twiss,MKD.O5L6.B1,mux);

reftcdqmingapb1 = chi6_nsig*sqrt(chi6_emitx*refbetxtcdqb1)-3e-4
                 -abs(refdxtcdqb1*chi6_dPoverP)-chi6_maxorbitdrift;
reftcdqgapb1 = chi6_nsig*sqrt(chi6_emitx*refbetxtcdqb1);

kq4.l6b1=kq4.l6b1;

tarir6b1=tar;
value,kqt13.l6b1,kqt12.l6b1,kqtl11.l6b1,kq10.l6b1,kq9.l6b1,kq8.l6b1,kq5.l6b1,kq4.l6b1,
kq4.r6b1,kq5.r6b1,kq8.r6b1,kq9.r6b1,kq10.r6b1,kqtl11.r6b1,kqt12.r6b1,kqt13.r6b1;


if (nomatch_beta==1){
  betxip6b1=refbetxip6b1 ;
  betyip6b1=refbetyip6b1 ;
  alfxip6b1=refalfxip6b1 ;
  alfyip6b1=refalfyip6b1 ;
};

if (nomatch_dx==1){
  dxip6b1=refdxip6b1;
};

if (nomatch_dpx==1){
  dpxip6b1=refdpxip6b1;
};

value,kq4.l6b1+0.004833837730000;
value,refdmuxkickb1;
value,refdmuxkickb1_tcsg;
value,refdmuxkickb1;
value,refbetxtcdqb1,refbetytcdqb1,betxtcdsb1,refbetytcdsb1,refbetxtcsgb1,refbetytcsgb1;
value,refdxtcdqb1;
value,refbxdumpb1,refbydumpb1,refbdumpb1;

value,refdmuxkickb1_eds;
value,refdmuxkickb1_bds;
value,refdmuxkickb1_bdsa;
value,refmuxip6b1;
value,refmuyip6b1;

value,kq5.l6b1*scale;
value,kq5.r6b1*scale;

value,tarir6b1;

