scale = 23348.89927;
scmin := 0.03*7000./nrj;
sch=0.99;
qtlimit2 := sch*160.0/scale;
qtlimit3 := sch*200.0/scale;
qtlimit4 := sch*125.0/scale;
qtlimit5 := sch*120.0/scale;
qtlimit6 := sch*90.0/scale;


if(on_holdselect==0){ exec,select(4,34,45,b2); };

if(on_fixedip==1){
muxip4b2=2.1600 ;betxip4b2 = 2.28562579E+02  ;alfxip4b2 =-5.37701685E-01;
muyip4b2=1.7200 ;betyip4b2 = 3.02565988E+02  ;alfyip4b2 = 5.88782733E-01;
dxip4b2 = 0.0000 ;dpxip4b2 = 0.0000 ;
};

value,muxip4b2,betxip4b2,alfxip4b2;
value,muyip4b2,betyip4b2,alfyip4b2;
value,dxip4b2,dpxip4b2;


use,sequence=lhcb2,range=s.ds.l4.b2/e.ds.r4.b2;
match,      sequence=lhcb2, beta0=bir4b2;
weight,mux=10,muy=10;
constraint, sequence=lhcb2, range=ip4,dx=dxip4b2,dpx =dpxip4b2;
if (nomatch_ip==0){
constraint, sequence=lhcb2, range=ip4,betx=betxip4b2,bety=betyip4b2;
constraint, sequence=lhcb2, range=ip4,alfx=alfxip4b2,alfy=alfyip4b2;
};
constraint, sequence=lhcb2, range=e.ds.r4.b2,alfx=eir4b2->alfx,alfy=eir4b2->alfy;
constraint, sequence=lhcb2, range=e.ds.r4.b2,betx=eir4b2->betx,bety=eir4b2->bety;
constraint, sequence=lhcb2, range=e.ds.r4.b2,dx=eir4b2->dx,dpx=eir4b2->dpx;
constraint, sequence=lhcb2, range=e.ds.r4.b2,   mux=muxip4b2+eir4b2->mux;
constraint, sequence=lhcb2, range=e.ds.r4.b2,   muy=muyip4b2+eir4b2->muy;


if (nomatch_ip==1){
if(betx_el4b2>0){constraint,sequence=lhcb2,range=elens.5r4.b2,betx=betx_el4b2;};
if(bety_el4b2>0){constraint,sequence=lhcb2,range=elens.5r4.b2,bety=bety_el4b2;};
if(betx_bsrtb2>0){constraint,sequence=lhcb2,range=MU.A5L4.B2, betx=betx_bsrtb2;};
if(bety_bsrtb2>0){constraint,sequence=lhcb2,range=MU.A5L4.B2, bety=bety_bsrtb2;};
};




!constraint,sequence=lhcb2, range=mqm.7l4.b2,bety<540;


if(match_on_aperture==1){
  if(nrj>3000){
    betarc=1135;!1130;
    constraint,range=s.ds.l4.b2/e.ds.r4.b2, betx<betarc,bety<betarc;
  } else {
   constraint, sequence=lhcb2, range=mq.11l4.b2,bety<181.6+5+epsap;
   constraint, sequence=lhcb2, range=mqm.9l4.b2,bety<167.+0.7+5+epsap;
   constraint, sequence=lhcb2, range=mqm.7l4.b2,bety<174+8.0+epsap;
   constraint, sequence=lhcb2, range=mqy.6l4.b2,betx<495+25+epsap;
   constraint, sequence=lhcb2, range=mqy.5l4.b2,bety<533+25+epsap;
   constraint, sequence=lhcb2, range=mqy.5r4.b2,betx<450+20+epsap; !13.0528
   constraint, sequence=lhcb2, range=mqy.6r4.b2,bety<552+30+epsap;
   constraint, sequence=lhcb2, range=mqml.8r4.b2,bety<177.5-4+epsap;
   constraint, sequence=lhcb2, range=mqml.10r4.b2,bety<177.5+3.2+4+epsap;
   constraint, sequence=lhcb2, range=mq.11r4.b2,betx<181.5-1.6+2+epsap,dx<2.35;
  };
};
 vary,name=kqt13.l4b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l4b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l4b2, step=1.0E-6, lower=-qtlimit4, upper= qtlimit4;
 vary,name=kq10.l4b2,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.l4b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.l4b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq7.l4b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq6.l4b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq5.l4b2,    step=1.0E-6, lower=-qtlimit2, upper= qtlimit2*scmin;

 vary,name=kq5.r4b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq6.r4b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq7.r4b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq8.r4b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.r4b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq10.r4b2,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kqtl11.r4b2, step=1.0E-6, lower=-qtlimit4, upper= qtlimit4;
 vary,name=kqt12.r4b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt13.r4b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
!lmdif,calls=500,tolerance=1.e-21;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;-
endmatch;

exec,mk_irtwiss(4,b2);

refbetx_el4b2=table(twiss,elens.5r4.b2,betx);
refbety_el4b2=table(twiss,elens.5r4.b2,bety);
refbetx_bsrtb2=table(twiss,MU.A5l4.B2,betx);
refbety_bsrtb2=table(twiss,MU.A5l4.B2,bety);


value,table(twiss,BPMCS.9L4.B2,bety),table(twiss,BPMCS.7L4.B2,bety);
value,table(twiss,BPMCS.9R4.B2,betx),table(twiss,BPMCS.7R4.B2,betx);
value,table(twiss,MKQA.6L4.B2,betx),table(twiss,MKQA.6L4.B2,bety);
value,table(twiss,BQKH.A6R4.B2,betx),table(twiss,BQKV.6R4.B2,bety);
value,table(twiss,ADTKV.D5L4.B2,bety),table(twiss,ADTKV.C5L4.B2,bety),table(twiss,ADTKV.B5L4.B2,bety),table(twiss,ADTKV.A5L4.B2,bety);
value,table(twiss,ADTKH.A5R4.B2,betx),table(twiss,ADTKH.B5R4.B2,betx),table(twiss,ADTKH.C5R4.B2,betx),table(twiss,ADTKH.D5R4.B2,betx);


tarir4b2=tar;
value,tarir4b2;
value,kqt13.l4b2,kqt12.l4b2,kqtl11.l4b2,kq10.l4b2,kq9.l4b2,kq8.l4b2,kq7.l4b2,kq6.l4b2,
kq5.l4b2,kq5.r4b2,kq6.r4b2,kq7.r4b2,kq8.r4b2,kq9.r4b2,kq10.r4b2,kqtl11.r4b2,kqt12.r4b2,kqt13.r4b2;

!muxip4b2=2.1600 ;betxip4b2 = 2.28562579E+02  ;alfxip4b2 =-5.37701685E-01;
!muyip4b2=1.7200 ;betyip4b2 = 3.02565988E+02  ;alfyip4b2 = 5.88782733E-01;
!dxip4b2 = 0.0000 ;dpxip4b2 = 0.0000 ;

!strategy for 11->22 : all but kq8.l4b2, disp and twiss as injection, control bety at  mqml.8r4.b2 (<190).
!strategy for 22->28 : all but kq8.l4b2, disp and twiss as injection, control bety at  mqml.8r4.b2 (<292).
!strategy for 22->82 : all but kq8.l4b2, disp and twiss as injection, control bety at  mqml.8r4.b2 (<200).


