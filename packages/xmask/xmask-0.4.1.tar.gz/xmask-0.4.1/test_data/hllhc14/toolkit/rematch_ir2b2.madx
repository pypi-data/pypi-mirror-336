scale = 23348.89927;
scmin := 0.03*7000./nrj;
qtlimitx28 := 1.0*225.0/scale;
qtlimitx15 := 1.0*205.0/scale;
qtlimit2 := 1.0*160.0/scale;
qtlimit3 := 1.0*200.0/scale;
qtlimit4 := 1.0*125.0/scale;
qtlimit5 := 1.0*120.0/scale;
qtlimit6 := 1.0*90.0/scale;


if(on_holdselect==0){
 exec,select(2,12,23,b2);
};

if(on_fixedip==1){
muxip2b2=3.0200;betxip2b2=10.0000;
muyip2b2=2.9000;betyip2b2=10.0000;
dxip2b2=0;dpxip2b2=0;
alfxip2b2=0;alfyip2b2=0;
};

value,muxip2b2,betxip2b2,alfxip2b2;
value,muyip2b2,betyip2b2,alfyip2b2;
value,dxip2b2,dpxip2b2;

kqx.r2:=-kqx.l2;

if (match_inj_tunes==1){
  KQX.L2=0.950981581300E-02;
}


use,sequence=lhcb2,range=s.ds.l2.b2/e.ds.r2.b2;
match,      sequence=lhcb2, beta0=bir2b2;
weight,mux=10,muy=10;
constraint, sequence=lhcb2, range=ip2,dx=dxip2b2,dpx =dpxip2b2;
constraint, sequence=lhcb2, range=ip2,betx=betxip2b2,bety=betyip2b2;
constraint, sequence=lhcb2, range=ip2,alfx=alfxip2b2,alfy=alfyip2b2;
constraint, sequence=lhcb2, range=e.ds.r2.b2,alfx=eir2b2->alfx,alfy=eir2b2->alfy;
constraint, sequence=lhcb2, range=e.ds.r2.b2,betx=eir2b2->betx,bety=eir2b2->bety;
constraint, sequence=lhcb2, range=e.ds.r2.b2,dx=eir2b2->dx,dpx=eir2b2->dpx;
constraint, sequence=lhcb2, range=e.ds.r2.b2,   mux=muxip2b2+eir2b2->mux;
constraint, sequence=lhcb2, range=e.ds.r2.b2,   muy=muyip2b2+eir2b2->muy;
if (is_thin==0){
if(scxir1>0.1){constraint, sequence=lhcb2, range=mqm.6r2.b2,bety<225;};
};
if(match_on_aperture==1){
constraint,sequence=lhcb2,range=mqml.10l2.b2,betx<175.5;
constraint,sequence=lhcb2,range=MQM.A7L2.B2,bety<160;
constraint,sequence=lhcb2,range=MQML.6L2.B2,betx<310.1+eps;
constraint,sequence=lhcb2,range= mqml.8r2.b2,bety<170;
constraint,sequence=lhcb2,range=mqml.10r2.b2,bety<175;
};
 vary,name=kqt13.l2b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l2b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l2b2, step=1.0E-6, lower=-qtlimit4, upper= qtlimit4;
 vary,name=kq10.l2b2,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.l2b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.l2b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq7.l2b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq6.l2b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq5.l2b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq4.l2b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
!vary,name=kqx.l2,      step=1.0E-6, lower= qtlimitx28*scmin, upper=qtlimitx28;
 vary,name=kq4.r2b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq5.r2b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;
 vary,name=kq6.r2b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq7.r2b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq8.r2b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.r2b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq10.r2b2,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kqtl11.r2b2, step=1.0E-6, lower=-qtlimit4, upper= qtlimit4;
 vary,name=kqt12.r2b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt13.r2b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;-
!lmdif,calls=200,tolerance=1.e-21;
endmatch;

exec,mk_irtwiss(2,b2);
value,kqx.l2-0.00950981581300000024;


if(on_aperture==1){
exec,plot_ap(2,b2,$nrj,$full);
};


tarir2b2=tar;
value,tarir2b2;

value,kqx.l2,kqx.r2,kqt13.l2b2,kqt12.l2b2,kqtl11.l2b2,kq10.l2b2,kq9.l2b2,kq8.l2b2,kq7.l2b2,
kq6.l2b2,kq5.l2b2,kq4.l2b2,kq4.r2b2,kq5.r2b2,kq6.r2b2,kq7.r2b2,kq8.r2b2,kq9.r2b2,kq10.r2b2,
kqtl11.r2b2,kqt12.r2b2,kqt13.r2b2;


kqx.r2    =kqx.r2    ;

value,-kq4.r2b1/kq4.r2b2;
value,-kq4.l2b1/kq4.l2b2;
value,-kq5.l2b1/kq5.l2b2;
value,-kq5.r2b1/kq5.r2b2;
value,-kq6.l2b1/kq6.l2b2;
value,-kq6.r2b1/kq6.r2b2;
value,-kq7.l2b1/kq7.l2b2;
value,-kq7.r2b1/kq7.r2b2;
value,-kq8.l2b1/kq8.l2b2;
value,-kq8.r2b1/kq8.r2b2;
value,-kq9.l2b1/kq9.l2b2;
value,-kq9.r2b1/kq9.r2b2;
value,-kq10.r2b1/kq10.r2b2;
value,-kq10.l2b1/kq10.l2b2;
value,tarir2b2;


!Strategy 11->22: fix the right, kq7.l2b2 & kqt12.l2b2
!Strategy 22->28: fix the right, kq7.l2b2 & kqt12.l2b2 (till 0.5/0.17) then kq13.l2b2 & kqt12.l2b2
!Strategy 22->82: touch the right in a way to be defined
!constraint,sequence=lhcb2,range=MQM.A7L2.B2,bety<320;
!constraint,sequence=lhcb2,range=MQML.6L2.B2,betx<600;



