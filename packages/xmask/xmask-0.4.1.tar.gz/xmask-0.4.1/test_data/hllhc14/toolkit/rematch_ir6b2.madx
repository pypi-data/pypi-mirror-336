scale = 23348.89927;
scmin := 0.03*7000./nrj;
sch=0.99; if (is_thin==1){sch=1;};
qtlimit2 := sch*160.0/scale;
qtlimit3 := sch*200.0/scale;
qtlimit4 := sch*125.0/scale;
qtlimit5 := sch*120.0/scale;
qtlimit6 := sch*90.0/scale;


if(on_holdselect==0){ exec,select(6,56,67,b2); };

if(on_fixedip==1){
muxip6b2 = 2.0000 ;betxip6b2= 187.749224;alfxip6b2 = 0.551968;
muyip6b2 = 2.0300 ;betyip6b2= 178.368556;alfyip6b2 =-0.607183;
dxip6b2 =  0.186;dpxip6b2 = -0.000256 ;
};

value,muxip6b2,betxip6b2,alfxip6b2;
value,muyip6b2,betyip6b2,alfyip6b2;
value,dxip6b2,dpxip6b2;

use,sequence=lhcb2,range=s.ds.l6.b2/e.ds.r6.b2;
match,      sequence=lhcb2, beta0=bir6b2;
weight,mux=10,muy=10;
if(nomatch_beta ==0){
constraint,sequence=lhcb2,range=ip6,betx=betxip6b2;
constraint,sequence=lhcb2,range=ip6,bety=betyip6b2;
constraint,sequence=lhcb2,range=ip6,alfx=alfxip6b2;
constraint,sequence=lhcb2,range=ip6,alfy=alfyip6b2;
};
if(nomatch_dx ==0){
constraint,sequence=lhcb2,range=ip6,dx=dxip6b2;
};
if(nomatch_dpx==0){
constraint,sequence=lhcb2,range=ip6,dpx=dpxip6b2;
};
!if(is_thin==1){
!  constraint,sequence=lhcb2,range=ip6,dx=dxip6b2,dpx =dpxip6b2;
!};
constraint,sequence=lhcb2,range=e.ds.r6.b2,alfx=eir6b2->alfx,alfy=eir6b2->alfy;
constraint,sequence=lhcb2,range=e.ds.r6.b2,betx=eir6b2->betx,bety=eir6b2->bety;
constraint,sequence=lhcb2,range=e.ds.r6.b2,dx=eir6b2->dx,dpx=eir6b2->dpx;
constraint,sequence=lhcb2,range=e.ds.r6.b2,mux=muxip6b2+eir6b2->mux;
constraint,sequence=lhcb2,range=e.ds.r6.b2,muy=muyip6b2+eir6b2->muy;
if(match_on_aperture==1){
!constraint,range=mq.12l6.b2,dx< 2.17;
constraint,range=mq.11l6.b2,bety< 178.5+epsap;
constraint,range=mqml.10l6.b2,betx< 175.2+epsap;
constraint,range=mqm.9l6.b2,bety< 180+epsap;
constraint,range=MQY.5L6.B2,bety<577+epsap+23;
constraint,range=MQY.4L6.B2,betx<577+epsap+23;
constraint,range=MQY.4R6.B2,bety<588+epsap+23;
constraint,range=MQY.5R6.B2,betx<588+epsap+23; !,dx<0.215;
constraint,range=mcbv.12r6.b2,bety< 180.2+epsap;
constraint,range=mqml.10r6.b2,bety< 178.5+1+1.4;
constraint,range=mqml.10l6.b2,betx< 175.2+epsap+1;
};
 vary,name=kqt13.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqt12.l6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
 vary,name=kqtl11.l6b2, step=1.0E-6, lower=-qtlimit4*400./550., upper=qtlimit4*400./550.;
 vary,name=kq10.l6b2,   step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq9.l6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq8.l6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq5.l6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq4.l6b2,    step=1.0E-6, lower= qtlimit2*scmin, upper= qtlimit2;

!vary,name=kq4.r6b2,    step=1.0E-6, lower=-qtlimit2, upper=-qtlimit2*scmin;
 vary,name=kq5.r6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq8.r6b2,    step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kq9.r6b2,    step=1.0E-6, lower= qtlimit3*scmin, upper= qtlimit3;
 vary,name=kq10.r6b2,   step=1.0E-6, lower=-qtlimit3, upper=-qtlimit3*scmin;
 vary,name=kqtl11.r6b2, step=1.0E-6, lower=-qtlimit4*300./550., upper=qtlimit4*300./550.;
if (is_thin==0){
 vary,name=kqt12.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
};
 vary,name=kqt13.r6b2,  step=1.0E-6, lower=-qtlimit5, upper= qtlimit5;
!lmdif,calls=200,tolerance=1.e-21;
jacobian,calls=jac_calls, tolerance=jac_tol, bisec=jac_bisec;
!simplex,  calls=15, tolerance=jac_tol;
endmatch;

exec,mk_irtwiss(6,b2);
chi6_nsig = 10.1;
chi6_dPoverP = 2e-4;
chi6_emitx=2.5e-6/(7000/0.9382720814);
chi6_maxorbitdrift = 0.6e-3;
al_dump=761;

refbetxip6b2=table(twiss,IP6,betx); refalfxip6b2=table(twiss,IP6,alfx);
refbetyip6b2=table(twiss,IP6,bety); refalfyip6b2=table(twiss,IP6,alfy);
refdxip6b2=table(twiss,IP6,dx); refdpxip6b2=table(twiss,IP6,dpx);

refdmuxkickb2_tcsg=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCSP.A4L6.B2,mux);
refdmuxkickb2_tcdqc=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.C4L6.B2,mux);
refdmuxkickb2_tcdqb=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.B4L6.B2,mux);
refdmuxkickb2_tcdqa=table(twiss,MKD.H5R6.B2,mux)-table(twiss,TCDQA.A4L6.B2,mux);
refdmuxkickb2=refdmuxkickb2_tcdqc;
if(abs(refdmuxkickb2_tcdqa-0.25)>abs(refdmuxkickb2-0.25)){
refdmuxkickb2=refdmuxkickb2_tcdqa;};
if(abs(refdmuxkickb2_tcdqb-0.25)>abs(refdmuxkickb2-0.25)){
refdmuxkickb2=refdmuxkickb2_tcdqb;};

refdxq5r6b2=abs(table(twiss,MQY.5R6.B2,dx));
refdxq4l6b2=abs(table(twiss,MQY.4L6.B2,dx));

refdxtcdqb2=table(twiss,TCDQA.A4L6.B2,dx);
refbetxtcdqb2=table(twiss,TCDQA.A4L6.B2,betx);
refbetytcdqb2=table(twiss,TCDQA.A4L6.B2,bety);
refbetxtcdsb2=table(twiss,TCDSA.4R6.B2,betx);
refbetytcdsb2=table(twiss,TCDSA.4R6.B2,bety);
refbetxtcsgb2=table(twiss,TCSP.A4L6.B2,betx);
refbetytcsgb2=table(twiss,TCSP.A4L6.B2,bety);
refbetxmkdb2 =table(twiss,MKD.H5R6.B2,betx);
refbetymkdb2 =table(twiss,MKD.H5R6.B2,bety);

refbxdumpb2=refbetxip6b2+2*al_dump*refalfxip6b2+
                        al_dump^2*(1+refalfxip6b2^2)/refbetxip6b2;
refbydumpb2=refbetyip6b2+2*al_dump*refalfyip6b2+
                        al_dump^2*(1+refalfyip6b2^2)/refbetyip6b2;
refbdumpb2=sqrt(refbxdumpb2*refbydumpb2);

refdmuxkickb2_bds=table(twiss,MKD.O5R6.B2,mux);
refdmuxkickb2_bdsa=table(twiss,MKD.A5R6.B2,mux);

reftcdqmingapb2 = chi6_nsig*sqrt(chi6_emitx*refbetxtcdqb2)-3e-4
                 -abs(refdxtcdqb2*chi6_dPoverP)-chi6_maxorbitdrift;
reftcdqgapb2 = chi6_nsig*sqrt(chi6_emitx*refbetxtcdqb2);


if (nomatch_beta==1){
  betxip6b2=refbetxip6b2 ;
  betyip6b2=refbetyip6b2 ;
  alfxip6b2=refalfxip6b2 ;
  alfyip6b2=refalfyip6b2 ;
};
if (nomatch_dx==1){
  dxip6b2 =refdxip6b2 ;
};

if (nomatch_dpx==1){
  dpxip6b2=refdpxip6b2 ;
};






kq4.r6b2=kq4.r6b2;

tarir6b2=tar;
value,kqt13.l6b2/qtlimit5,kqt12.l6b2/qtlimit5,kqtl11.l6b2,kq10.l6b2,kq9.l6b2,kq8.l6b2,kq5.l6b2,kq4.l6b2,
kq4.r6b2,kq5.r6b2,kq8.r6b2,kq9.r6b2,kq10.r6b2,kqtl11.r6b2,kqt12.r6b2/qtlimit5,kqt13.r6b2/qtlimit5;

value,kq4.r6b2+0.004833837730000;
value,refdmuxkickb2;
value,refdmuxkickb2_tcsg;
value,refdmuxkickb2_tcdq;
value,refbetxtcdqb2,refbetytcdqb2,refbetxtcdsb2,refbetytcdsb2,refbetxtcsgb2,refbetytcsgb2;
value,refdxtcdqb2;
value,refbxdumpb2,refbydumpb2,refbdumpb2;

value,refdmuxkickb2_bds;
value,refmuxip6b2;
value,refmuyip6b2;

value,kq5.l6b2*scale;
value,kq5.r6b2*scale;

value,tarir6b2;

!muxip6b2 = 2.0000 ;betxip6b2= 1.96000000E+02 ;alfxip6b2 = 5.98285418E-01  ;
!muyip6b2 = 2.0300 ;betyip6b2= 1.79082800E+02;alfyip6b2 =-5.41937487E-01 ;
!dxip6b2 =  1.74610979E-01;dpxip6b2 = 1.56146811E-03 ;
!strategy for 11->22 : use all (kq4.r6b2 fixed),alfas,dx as injection,dpx=0,betas not constrained
!strategy for 22->28 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained
!strategy for 22->82 : use all (kq4.r6b2 fixed),alfas, dx as injection,dpx=0,betas not constrained (around 320/180)


